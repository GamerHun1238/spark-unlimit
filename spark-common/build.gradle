plugins {
    id 'com.google.protobuf' version '0.9.4'
}

license {
    exclude '**/sampler/async/jfr/**'
    exclude {
        it.file.toString().startsWith(buildDir.toString())
    }
}

dependencies {
    api project(':spark-api')
    implementation 'com.github.jvm-profiling-tools:async-profiler:v2.8.3'
    implementation 'org.ow2.asm:asm:9.7'
    implementation 'net.bytebuddy:byte-buddy-agent:1.14.17'
    implementation 'com.google.protobuf:protobuf-javalite:4.27.1'

    implementation('me.lucko:bytesocks-java-client:1.0-20230828.145440-5') {
        exclude(module: 'slf4j-api')
    }

    api('net.kyori:adventure-api:4.13.1') {
        exclude(module: 'adventure-bom')
        exclude(module: 'checker-qual')
        exclude(module: 'annotations')
    }
    api('net.kyori:adventure-text-serializer-gson:4.13.1') {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
        exclude(module: 'gson')
    }
    api('net.kyori:adventure-text-serializer-legacy:4.13.1') {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
    }
    implementation('net.kyori:adventure-text-feature-pagination:4.0.0-SNAPSHOT') {
        exclude(module: 'adventure-api')
    }
    compileOnly 'com.google.code.gson:gson:2.7'
    compileOnly 'com.google.guava:guava:19.0'
    compileOnly 'org.checkerframework:checker-qual:3.44.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.27.1'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
